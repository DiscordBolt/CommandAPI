group 'com.discordbolt.api'
version '2.2'

buildscript {
    ext {
        discord4jVersion = "v3-SNAPSHOT"
    }
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    toolVersion "8.10.1"
    configFile = 'src/test/resources/checkstyle.xml' as File
}

repositories {
    mavenCentral()
    // Discord 4J
    maven { url  "https://jitpack.io" }
    // Reactor Fix
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    // Discord4J
    api group: 'com.discord4j.discord4j', name: 'discord4j-core', version: "$discord4jVersion"
    // Reflection (For finding commands)
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'
    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    // @NotNull annotations
    implementation group: 'org.checkerframework', name: 'checker', version: '2.1.12'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
    testCompile group: 'io.projectreactor', name: 'reactor-test', version: '3.1.0.RELEASE'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

wrapper {
    gradleVersion = '4.9'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}